
Option Explicit

Public ColsSpecialChar As Collection
Public ColsNoNumbers As Collection
Public ColsEmail As Collection

Private Sub UserForm_Initialize()

    Dim ws As Worksheet
    Dim lastCol As Long, i As Long
    Dim headerName As String

    ' DEFAULT SELECTIONS (hardcoded)
    Dim defSpecial As Variant
    Dim defNoNumbers As Variant
    Dim defEmail As Variant

    defSpecial = Array("Name", "Address Line 1")
    defNoNumbers = Array("Name", "Address Line 3", "Address Line 4")
    defEmail = Array("Email", "Email Address")

    Set ws = ThisWorkbook.Sheets("Sheet1")
    lastCol = ws.Cells(1, ws.Columns.Count).End(xlToLeft).Column

    For i = 1 To lastCol
        headerName = Trim(ws.Cells(1, i).Value)
        If headerName <> "" Then

            lstSpecialChar.AddItem headerName
            lstNoNumbers.AddItem headerName
            lstEmail.AddItem headerName

            If IsInArraySafe(headerName, defSpecial) Then
                lstSpecialChar.Selected(lstSpecialChar.ListCount - 1) = True
            End If

            If IsInArraySafe(headerName, defNoNumbers) Then
                lstNoNumbers.Selected(lstNoNumbers.ListCount - 1) = True
            End If

            If IsInArraySafe(headerName, defEmail) Then
                lstEmail.Selected(lstEmail.ListCount - 1) = True
            End If

        End If
    Next i

End Sub


Private Sub btnRun_Click()

    Dim i As Long

    Set ColsSpecialChar = New Collection
    Set ColsNoNumbers = New Collection
    Set ColsEmail = New Collection

    For i = 0 To lstSpecialChar.ListCount - 1
        If lstSpecialChar.Selected(i) Then ColsSpecialChar.Add lstSpecialChar.List(i)
    Next i

    For i = 0 To lstNoNumbers.ListCount - 1
        If lstNoNumbers.Selected(i) Then ColsNoNumbers.Add lstNoNumbers.List(i)
    Next i

    For i = 0 To lstEmail.ListCount - 1
        If lstEmail.Selected(i) Then ColsEmail.Add lstEmail.List(i)
    Next i

    Me.Hide
End Sub


Private Sub btnCancel_Click()
    Set ColsSpecialChar = Nothing
    Set ColsNoNumbers = Nothing
    Set ColsEmail = Nothing
    Me.Hide
End Sub
